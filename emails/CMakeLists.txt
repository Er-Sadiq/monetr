set(EMAIL_TEMPLATE_INPUT_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/src")
set(EMAIL_TEMPLATE_OUTPUT_DIRECTORY "${CMAKE_SOURCE_DIR}/server/communication/email_templates")

# WARNING: This does not exclude any spec or story files.
file(GLOB_RECURSE EMAIL_TEMPLATE_INPUTS "${EMAIL_TEMPLATE_INPUT_DIRECTORY}/*.tsx")

set(HTML_EMAIL_TEMPLATES "${EMAIL_TEMPLATE_INPUTS}")
list(TRANSFORM HTML_EMAIL_TEMPLATES REPLACE "${EMAIL_TEMPLATE_INPUT_DIRECTORY}" "${EMAIL_TEMPLATE_OUTPUT_DIRECTORY}")
list(TRANSFORM HTML_EMAIL_TEMPLATES REPLACE ".tsx" ".html")

set(TEXT_EMAIL_TEMPLATES "${EMAIL_TEMPLATE_INPUTS}")
list(TRANSFORM TEXT_EMAIL_TEMPLATES REPLACE "${EMAIL_TEMPLATE_INPUT_DIRECTORY}" "${EMAIL_TEMPLATE_OUTPUT_DIRECTORY}")
list(TRANSFORM TEXT_EMAIL_TEMPLATES REPLACE ".tsx" ".txt")

add_custom_command(
  OUTPUT ${HTML_EMAIL_TEMPLATES} ${TEXT_EMAIL_TEMPLATES}
  BYPRODUCTS ${HTML_EMAIL_TEMPLATES} ${TEXT_EMAIL_TEMPLATES}
  COMMAND ${REACT_EMAIL_EXECUTABLE} export --dir src --outDir ${EMAIL_TEMPLATE_OUTPUT_DIRECTORY}
  COMMAND ${REACT_EMAIL_EXECUTABLE} export --plainText --dir src --outDir ${EMAIL_TEMPLATE_OUTPUT_DIRECTORY}
  COMMENT "Building email templates from React"
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
  DEPENDS
    tools.react-email
    ${EMAIL_TEMPLATE_INPUTS}
)

add_custom_target(
  build.email
  COMMAND exit
  DEPENDS
    ${HTML_EMAIL_TEMPLATES}
    ${TEXT_EMAIL_TEMPLATES}
)

add_custom_target(
  development.email
  COMMENT "Running the local development tools for email templates"
  COMMAND ${REACT_EMAIL_EXECUTABLE} dev --dir src
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
  DEPENDS
    tools.react-email
)
